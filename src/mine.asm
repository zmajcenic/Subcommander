; rutina pokusa aktivirati neaktivnu minu za brod u danom retku
; IX = row inst
; vraca H = y koordinata, L = x koordinata, cf=1 ako smo spremni za pucanje mine
NADJI_BROD_ZA_MINU_BYROW:
	LD	A, (IX + OFF_BROD_INST_ID)
	OR	A
	JR	NZ, NADJI_BROD_ZA_MINU_BYROW_L1
	; cijeli row neaktivan
	XOR	A
	RET
NADJI_BROD_ZA_MINU_BYROW_L1:
	LD	DE, OFF_BROD_INST_SPRITE
	PUSH	IX
	POP	IY
	ADD	IY, DE
	LD	DE, SIZEOF_SPRITE
	LD	B, 4
NADJI_BROD_ZA_MINU_BYROW_L2:
	LD	A, (IY + OFF_SPRITE_AKTIV_FLAG)
	OR	A
	JR	Z, NADJI_BROD_ZA_MINU_BYROW_L3
	; aktivan brod
	LD	HL, (CIVILIAN_TARGET_INST)
	LD	A, H
	OR	L
	JR	Z, NADJI_BROD_ZA_MINU_BYROW_L4
	; postoji civilian target
	LD	HL, (CIVILIAN_TARGET_SPRITE)
	PUSH	DE
	PUSH	IY
	POP	DE
	XOR	A
	SBC	HL, DE
	POP	DE
	JR	Z, NADJI_BROD_ZA_MINU_BYROW_L3; to je civilian target, on ne baca mine
NADJI_BROD_ZA_MINU_BYROW_L4:
	; brod nije civilian target
	CALL	GET_RND_NUM
	CP	220
	JR	C, NADJI_BROD_ZA_MINU_BYROW_L3
	; prosli smo %, provjeri da li je cijeli brod vidljiv
	LD	L, (IY + OFF_SPRITE_X)
	LD	H, (IY + OFF_SPRITE_X + 1)
	BIT	7, H
	JR	NZ, NADJI_BROD_ZA_MINU_BYROW_L3; brod.x < 0
	PUSH	DE
	LD	E, (IX + OFF_BROD_INST_DIM_X)
	SLA	E
	SLA	E
	SLA	E
	ADD	HL, DE
	LD	A, H
	CP	1
	LD	A, E
	POP	DE
	JR	NC, NADJI_BROD_ZA_MINU_BYROW_L3; brod.x + width > 256
	; nasli smo ga
	SRL	A; ispucaj sa sredine broda
	ADD	A, (IY + OFF_SPRITE_X)
	LD	L, A; imamo x
	LD	A, (IX + OFF_BROD_INST_DIM_Y)
	SLA	A
	SLA	A
	ADD	A, (IY + OFF_SPRITE_Y)
	LD	H, A; imamo y, na pola visine broda
	SCF	; nasli smo
	RET
NADJI_BROD_ZA_MINU_BYROW_L3:
	ADD	IY, DE
	DJNZ	NADJI_BROD_ZA_MINU_BYROW_L2
	XOR	A; prosli smo brodove i nismo nasli
	RET
	
; rutina za nalazenje prikladnog broda za ispucavanje mine
; vraca H = y koordinata, L = x koordinata, cf=1 ako smo spremni za pucanje mine
NADJI_BROD_ZA_MINU:
	LD	IX, ROW2_BROD_INST
	CALL	NADJI_BROD_ZA_MINU_BYROW
	RET	C
	LD	IX, ROW5_BROD_INST
	CALL	NADJI_BROD_ZA_MINU_BYROW
	RET	C
	LD	IX, ROW8_BROD_INST
	CALL	NADJI_BROD_ZA_MINU_BYROW
	RET	C
	LD	IX, ROW10_BROD_INST
	CALL	NADJI_BROD_ZA_MINU_BYROW
	RET; 	ne bacamo mine iz row 13 jer je preblizu floatsome

; rutina u glavnoj petlji za aktiviranje mina
CHECK_FIRE_MINE:
	LD	A, (MINA1_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ACTIVE)
	OR	A
	JR	NZ, CHECK_FIRE_MINE_L1
	CALL	NADJI_BROD_ZA_MINU
	JR	NC, CHECK_FIRE_MINE_L1
	DI
	LD	A, H
	LD	(MINA1_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	H, 0
	LD	(MINA1_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	A, MINA1_HW_SPRITE_INDEX
	LD	BC, MINA1_HW_SPRITE
	LD	HL, MINA_ANIMACIJA
	LD	IX, MINA1_ANIMACIJA_INST
	CALL	SET_ANIMACIJA_INST_TO_INIT
	EI
CHECK_FIRE_MINE_L1:
	LD	A, (MINA2_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ACTIVE)
	OR	A
	RET	NZ
	CALL	NADJI_BROD_ZA_MINU
	RET	NC
	DI
	LD	A, H
	LD	(MINA2_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	H, 0
	LD	(MINA2_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	A, MINA2_HW_SPRITE_INDEX
	LD	BC, MINA2_HW_SPRITE
	LD	HL, MINA_ANIMACIJA
	LD	IX, MINA2_ANIMACIJA_INST
	CALL	SET_ANIMACIJA_INST_TO_INIT
	EI
	RET

; pomocna funkcija za provjeru da li je eksplozija mine zahvatila igraca
; D = player.x
; E = player.x + width
; C = mina.x
; B = radius eksplozije
; vraca cf=1 ako igrac zahvacen
CHECK_MINE_EXPLOSION_PLAYER_HIT:
	LD	A, C
	ADD	A, B
	CP	D
	JR	C, CHECK_MINE_EXPLOSION_PLAYER_HIT_L1
	LD	A, C
	SUB	B
	CP	D
	JR 	NC, CHECK_MINE_EXPLOSION_PLAYER_HIT_L1
	; zadovoljeno mina.x-r < player.x < mina.x+r
	SCF
	RET
CHECK_MINE_EXPLOSION_PLAYER_HIT_L1:
	LD	A, C
	ADD	A, B
	CP	E
	JR	C, CHECK_MINE_EXPLOSION_PLAYER_HIT_L2
	LD	A, C
	SUB	B
	CP	E
	JR	NC, CHECK_MINE_EXPLOSION_PLAYER_HIT_L2
	; zadovoljeno mina.x-r < player.x+width < mina.x+r
	SCF
	RET
CHECK_MINE_EXPLOSION_PLAYER_HIT_L2:
	LD	A, D
	CP	C
	JR	NC, CHECK_MINE_EXPLOSION_PLAYER_HIT_L3
	LD	A, E
	CP	C
	JR	C, CHECK_MINE_EXPLOSION_PLAYER_HIT_L3
	; zadovoljeno player.x < mina.x < player.x+width
	SCF
	RET
CHECK_MINE_EXPLOSION_PLAYER_HIT_L3:
	XOR	A
	RET

; pomocna funkcija za aktivaciju eksplozije mine 1
EXPLODE_MINE1:
	DI
	LD	HL, (MINA1_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	DE, 8
	XOR	A
	SBC	HL, DE
	LD	(MINA1_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	A, (MINA1_HW_SPRITE + OFF_HW_SPRITE_Y)
	; zbog mogucnosti da nam mina sklizne preduboko
	; ako eksplodira ispod 182, postavi y fiksno na 176
	LD	A, (MINA1_HW_SPRITE + OFF_HW_SPRITE_Y)
	CP	182
	JR	C, EXPLODE_MINE1_L1
	LD	A, 176
	JR	EXPLODE_MINE1_L2
EXPLODE_MINE1_L1:
	SUB	8
EXPLODE_MINE1_L2:
	LD	(MINA1_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	HL, ANIMACIJA_EKSPLOZIJA_MINE
	LD	BC, MINA1_HW_SPRITE
	LD	IX, MINA1_ANIMACIJA_INST
	LD	A, MINA1_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; sound
	LD	A, (GAMETIMER_ENDED)
	OR	A
	JR	NZ, EXPLODE_MINE1_L3
	LD	A, 2; sound effect number
	LD	C, 1; channel
	LD	B, 0; volume, 0=full
	CALL	SOUNDS_PLAYER_PLAY
EXPLODE_MINE1_L3:
	EI
	RET
; pomocna funkcija za aktivaciju eksplozije mine 2
EXPLODE_MINE2:
	DI
	LD	HL, (MINA2_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	DE, 8
	XOR	A
	SBC	HL, DE
	LD	(MINA2_HW_SPRITE + OFF_HW_SPRITE_X), HL
	; zbog mogucnosti da nam mina sklizne preduboko
	; ako eksplodira ispod 182, postavi y fiksno na 176
	LD	A, (MINA2_HW_SPRITE + OFF_HW_SPRITE_Y)
	CP	182
	JR	C, EXPLODE_MINE2_L1
	LD	A, 176
	JR	EXPLODE_MINE2_L2
EXPLODE_MINE2_L1:
	SUB	8
EXPLODE_MINE2_L2:
	LD	(MINA2_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	HL, ANIMACIJA_EKSPLOZIJA_MINE
	LD	BC, MINA2_HW_SPRITE
	LD	IX, MINA2_ANIMACIJA_INST
	LD	A, MINA2_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; sound
	LD	A, (GAMETIMER_ENDED)
	OR	A
	JR	NZ, EXPLODE_MINE2_L3
	LD	A, 2
	LD	C, 1
	LD	B, 0
	CALL	SOUNDS_PLAYER_PLAY
EXPLODE_MINE2_L3:
	EI
	RET

; pomocna funkcija za provjeru da li je mina zahvatila igraca 1
; potrebno prije poziva u C postaviti mina.x
CHECK_MINE_EXPLOSION_PLAYER1_HIT:
	LD	A, (ROW23_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	D, A
	LD	A, (ROW23_BROD_INST + OFF_BROD_INST_DIM_Y)
	SLA	A
	SLA	A
	SLA	A
	ADD	A, D
	LD	E, A
	LD	B, 8
	JP	CHECK_MINE_EXPLOSION_PLAYER_HIT
; pomocna funkcija za provjeru da li je mina zahvatila igraca 2
; potrebno prije poziva u C postaviti mina.x
CHECK_MINE_EXPLOSION_PLAYER2_HIT:
	LD	A, (ROW22_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	D, A
	LD	A, (ROW22_BROD_INST + OFF_BROD_INST_DIM_Y)
	SLA	A
	SLA	A
	SLA	A
	ADD	A, D
	LD	E, A
	LD	B, 8
	JP	CHECK_MINE_EXPLOSION_PLAYER_HIT

; pomocna funkcija za disable playera 1 i oduzimanje 555 bodova
PLAYER1_MINE_HIT:
	DI
	LD	A, 1
	LD	(PLAYER1_DISABLED), A
	LD	HL, (JIFFY)
	LD	(PLAYER1_DISABLED_BEGIN_TIME), HL
	; aktiviraj animacije ostecene podmornice i minus bodova
	LD	HL, (ROW23_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	DE, 9
	ADD	HL, DE
	LD	(OSTECENA_PODMORNICA1_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	HL, (ROW23_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	E, 3
	ADD	HL, DE
	LD	(MINA_POGODAK_MINUS_BODOVI_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	A, 176
	LD	(OSTECENA_PODMORNICA1_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	A, 160
	LD	(MINA_POGODAK_MINUS_BODOVI_HW_SPRITE + OFF_HW_SPRITE_Y), A
	;LD	A, 6
	;LD	(OSTECENA_PODMORNICA1_HW_SPRITE + OFF_HW_SPRITE_COLOR), A
	LD	HL, ANIMACIJA_OSTECENA_PODMORNICA
	LD	BC, OSTECENA_PODMORNICA1_HW_SPRITE
	LD	IX, OSTECENA_PODMORNICA1_ANIMACIJA_INST
	LD	A, OSTECENA_PODMORNICA1_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; ako smo na izlasku iz igre ne oduzimaj bodove
	LD	A, (GAMETIMER_ENDED)
	OR	A
	JR	NZ, PLAYER1_MINE_HIT_EXIT
	LD	HL, ANIMACIJA_MINA_POGODAK_MINUS_BODOVI
	LD	BC, MINA_POGODAK_MINUS_BODOVI_HW_SPRITE
	LD	IX, MINA_POGODAK_MINUS_BODOVI_ANIMACIJA_INST
	LD	A, MINA_POGODAK_MINUS_BODOVI_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; oduzmi 555 bodova
	LD	HL, (PLAYER1_SCORE)
	LD	DE, 555
	XOR	A
	SBC	HL, DE
	BIT	7, H
	JR	Z, PLAYER1_MINE_HIT_L1
	; u minusu smo
	LD	HL, 0
PLAYER1_MINE_HIT_L1:
	LD	(PLAYER1_SCORE), HL
	LD	A, 1
	LD	(PLAYER1_SCORE_UPDATED), A
PLAYER1_MINE_HIT_EXIT:
	EI
	RET

; pomocna funkcija za disable playera 2 i oduzimanje 500 bodova
PLAYER2_MINE_HIT:
	DI
	LD	A, 1
	LD	(PLAYER2_DISABLED), A
	LD	HL, (JIFFY)
	LD	(PLAYER2_DISABLED_BEGIN_TIME), HL
	; aktiviraj animaciju
	LD	HL, (ROW22_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	DE, 9
	ADD	HL, DE
	LD	(OSTECENA_PODMORNICA2_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	HL, (ROW22_BROD_INST + OFF_BROD_INST_SPRITE + OFF_SPRITE_X)
	LD	E, 3
	ADD	HL, DE
	LD	(MINA_POGODAK_MINUS_BODOVI_HW_SPRITE + OFF_HW_SPRITE_X), HL
	LD	A, 168
	LD	(OSTECENA_PODMORNICA2_HW_SPRITE + OFF_HW_SPRITE_Y), A
	LD	A, 160
	LD	(MINA_POGODAK_MINUS_BODOVI_HW_SPRITE + OFF_HW_SPRITE_Y), A
	;LD	A, 6
	;LD	(OSTECENA_PODMORNICA1_HW_SPRITE + OFF_HW_SPRITE_COLOR), A
	LD	HL, ANIMACIJA_OSTECENA_PODMORNICA
	LD	BC, OSTECENA_PODMORNICA2_HW_SPRITE
	LD	IX, OSTECENA_PODMORNICA2_ANIMACIJA_INST
	LD	A, OSTECENA_PODMORNICA2_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; ako smo na izlasku iz igre ne oduzimaj bodove
	LD	A, (GAMETIMER_ENDED)
	OR	A
	JR	NZ, PLAYER2_MINE_HIT_EXIT
	LD	HL, ANIMACIJA_MINA_POGODAK_MINUS_BODOVI
	LD	BC, MINA_POGODAK_MINUS_BODOVI_HW_SPRITE
	LD	IX, MINA_POGODAK_MINUS_BODOVI_ANIMACIJA_INST
	LD	A, MINA_POGODAK_MINUS_BODOVI_HW_SPRITE_INDEX
	CALL	SET_ANIMACIJA_INST_TO_INIT
	; oduzmi 555 bodova
	LD	HL, (PLAYER2_SCORE)
	LD	DE, 555
	XOR	A
	SBC	HL, DE
	BIT	7, H
	JR	Z, PLAYER2_MINE_HIT_L1
	; u minusu smo
	LD	HL, 0
PLAYER2_MINE_HIT_L1:
	LD	(PLAYER2_SCORE), HL
	LD	A, 1
	LD	(PLAYER2_SCORE_UPDATED), A
PLAYER2_MINE_HIT_EXIT:
	EI
	RET

; funkcija koja se poziva u glavnom programu i provjerava da li je mina udarila u floatsome
; dosla do dna ekrana i njena eksplozija zahvatila igrace
CHECK_MINE_COLLISIONS:
	LD	A, (MINA1_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ACTIVE)
	OR	A
	JR	Z, CHECK_MINE_COLLISIONS_L3
	; mina 1 je aktivna
	;provjeri udar u floatsome
	; provjeri da li se radi o ispaljenoj mini koja tone
	LD	HL, (MINA1_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ANIMACIJA)
	LD	DE, MINA_ANIMACIJA
	XOR	A
	SBC	HL, DE
	;RET	NZ
	JR	NZ, CHECK_MINE_COLLISIONS_L3
	LD	L, 0
	LD	IX, MINA1_HW_SPRITE
	CALL	COLLISION_DETECTION
	JR	NC, CHECK_MINE_COLLISIONS_L1
	; pogodjen floatsome, aktiviraj eksploziju
	CALL	EXPLODE_MINE1
	JR	CHECK_MINE_COLLISIONS_L3
CHECK_MINE_COLLISIONS_L1:
	; provjeri da li je mina ispod 182
	LD	A, (MINA1_HW_SPRITE + OFF_HW_SPRITE_Y)
	CP	182
	JR	C, CHECK_MINE_COLLISIONS_L3
	; dosli smo do dna, eksplodiraj
	CALL	EXPLODE_MINE1
	; provjeri da li smo zahvatili igraca 1
	LD	A, (MINA1_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	C, A
	CALL	CHECK_MINE_EXPLOSION_PLAYER1_HIT
	JR	NC, CHECK_MINE_COLLISIONS_L2
	; pogodili smo player 1
	CALL	PLAYER1_MINE_HIT
CHECK_MINE_COLLISIONS_L2:
	; provjeri da li smo zahvatili igraca 2, ako je aktivan
	LD	A, (TWO_PLAYER_GAME)
	OR	A
	JR	Z, CHECK_MINE_COLLISIONS_L3
	LD	A, (MINA1_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	C, A
	CALL	CHECK_MINE_EXPLOSION_PLAYER2_HIT
	JR	NC, CHECK_MINE_COLLISIONS_L3
	; pogodili smo player 2
	CALL	PLAYER2_MINE_HIT	
	
	; mina 2
CHECK_MINE_COLLISIONS_L3:
	LD	A, (MINA2_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ACTIVE)
	OR	A
	RET	Z
	; mina 2 je aktivna
	; provjeri da li se radi o ispaljenoj mini koja tone
	LD	HL, (MINA2_ANIMACIJA_INST + OFF_ANIMACIJA_INST_ANIMACIJA)
	LD	DE, MINA_ANIMACIJA
	XOR	A
	SBC	HL, DE
	RET	NZ
	;provjeri udar u floatsome
	LD	L, 0
	LD	IX, MINA2_HW_SPRITE
	CALL	COLLISION_DETECTION
	JR	NC, CHECK_MINE_COLLISIONS_L4
	; pogodjen floatsome, aktiviraj eksploziju
	CALL	EXPLODE_MINE2
	RET
CHECK_MINE_COLLISIONS_L4:
	; provjeri da li je mina ispod 182
	LD	A, (MINA2_HW_SPRITE + OFF_HW_SPRITE_Y)
	CP	182
	RET	C
	; dosli smo do dna, eksplodiraj
	CALL	EXPLODE_MINE2
	; provjeri da li smo zahvatili igraca 1
	LD	A, (MINA2_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	C, A
	CALL	CHECK_MINE_EXPLOSION_PLAYER1_HIT
	JR	NC, CHECK_MINE_COLLISIONS_L5
	; pogodili smo player 1
	CALL	PLAYER1_MINE_HIT
CHECK_MINE_COLLISIONS_L5:
	; provjeri da li smo zahvatili igraca 2, ako je aktivan
	LD	A, (TWO_PLAYER_GAME)
	OR	A
	RET	Z
	LD	A, (MINA2_HW_SPRITE + OFF_HW_SPRITE_X)
	LD	C, A
	CALL	CHECK_MINE_EXPLOSION_PLAYER2_HIT
	RET	NC
	; pogodili smo player 2
	JP	PLAYER2_MINE_HIT	